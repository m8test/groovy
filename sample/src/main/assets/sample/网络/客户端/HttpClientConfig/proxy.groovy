package sample.网络.客户端.HttpClientConfig

/*
功能:
+ 设置Http或者Socks代理.

定义:
+ HttpClientConfig proxy(String type, String host, int port)

参数:
+ String type - 代理类型,可以是 http, socks 以及 direct (默认)
+ String host - 代理主机名
+ int port - 代理端口

返回值:
+ HttpClientConfig - 返回对象本身方便链式调用

示例代码:
+ 在8080端口开启一个服务器作为代理服务器,通过 HttpClient 访问任意网址都会转发到该服务器.
 */
// Floaty.showConsoleWindow() 用于显示控制台悬浮窗口
$floaty.showConsoleWindow()
// 在8080端口新建一个服务器
def server = $httpServerManager.newServer(8080) {
    // delegate -> HttpServerConfig
    // HttpServerConfig.routing() 用于配置服务器路由信息
    routing {
        // delegate -> HttpServerRouting
        // HttpServerRouting.get() 用于配置Get路由, {...} 为通配符,可以匹配任意路径.
        get("/{...}") {
            // delegate -> HttpServerCall
            // HttpServerCall.request() 可以获得请求对象HttpServerRequest
            def request = request()
            // HttpServerRequest.path() 可获得访问路径
            def path = request.path()
            $console.log("Path->$path")
            // HttpServerCall.response() 可以获取响应对应HttpServerResponse
            def response = response()
            // HttpServerResponse.textBody() 可以发送文本到客户端
            response.textBody(path)
        }
    }
}
// HttpServer.start() 用于启动服务器,如果端口已经被占用了则会启动失败,报错 "Address already in use"
server.start()
// 新建一个Http客户端, HttpClientManager.newClient() 用于创建一个Http客户端
def client = $httpClientManager.newClient {
    // HttpClientConfig.proxy() 用于配置代理服务器
    proxy("http", "localhost", 8080)
}
// HttpClient.request() 会返回一个 HttpClientResponse 对象,封装了Http响应信息
def response = client.request("http://www.google.com/") {
    // delegate -> HttpClientRequest
    // HttpClientRequest.method() 用于配置请求方法,此处为配置为Get请求.除此之外,还有post,put,patch,delete,head,options等方法
    method "get"
}
// HttpClientResponse.textBody() 可以将服务器返回结果转成文本
def textResult = response.textBody()
$console.log("服务器返回结果:$textResult")
// 断言返回结果为"http://www.google.com/"
$assert.assertTrue(textResult == "http://www.google.com/", "返回结果为 http://www.google.com/")
// HttpClient.request() 会返回一个 HttpClientResponse 对象,封装了Http响应信息
response = client.request("http://www.youtube.com/") {
    // delegate -> HttpClientRequest
    // HttpClientRequest.method() 用于配置请求方法,此处为配置为Get请求.除此之外,还有post,put,patch,delete,head,options等方法
    method "get"
}
// HttpClientResponse.textBody() 可以将服务器返回结果转成文本
textResult = response.textBody()
$console.log("服务器返回结果:$textResult")
// 断言返回结果为"http://www.youtube.com/"
$assert.assertTrue(textResult == "http://www.youtube.com/", "返回结果为 http://www.youtube.com/")
// HttpServer.stop() 用于停止服务器.
server.stop()