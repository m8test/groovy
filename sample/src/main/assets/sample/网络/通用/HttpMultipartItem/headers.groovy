package sample.网络.通用.HttpMultipartItem

/*
功能:
+ 获取当前part中的请求头.

定义:
+ JSONObject headers()

参数:
+ 无

返回值:
+ JSONObject - 包含当前part请求头的对象.

示例代码:
+ 在8080端口开启一个服务器,通过 HttpMultipartItem.headers() 获取当前part中的请求头对象.
 */
// Floaty.showConsoleWindow() 用于显示控制台悬浮窗口
$floaty.showConsoleWindow()
// 在8080端口新建一个服务器
def server = $httpServerManager.newServer(8080) {
    // delegate -> HttpServerConfig
    // HttpServerConfig.routing() 用于配置服务器路由信息
    routing {
        // delegate -> HttpServerRouting
        // HttpServerRouting.post() 用于配置Post路由
        post("/headers") {
            // delegate -> HttpServerCall
            // HttpServerCall.request() 可以获得请求对象HttpServerRequest
            def request = request()
            // HttpServerRequest.path() 可获得访问路径,此处为 "/headers"
            def path = request.path()
            $assert.assertTrue(path == "/headers")
            $console.log("Path->$path")
            // HttpServerRequest.multipartBody() 可以接收multipart类型的数据, 返回值类型为 List<HttpMultipartItem>
            def multipart = request.multipartBody()
            def partItem = multipart[0]
            // HttpMultipartItem.headers() 用于获取当前part中的请求头
            def headers = partItem.headers()
            $assert.assertTrue(headers.get("header1") == "value1", "header1")
            $assert.assertTrue(headers.get("header2") == "value2", "header2")
            // HttpServerCall.response() 可以获取响应对应HttpServerResponse
            def response = response()
            // HttpServerResponse.textBody() 可以发送文本到客户端
            response.textBody(path)
        }
    }
}
// HttpServer.start() 用于启动服务器,如果端口已经被占用了则会启动失败,报错 "Address already in use"
server.start()
// 新建一个Http客户端, HttpClientManager.newClient() 用于创建一个Http客户端
def client = $httpClientManager.newClient()
// HttpClient.postMultipart() 会返回一个 HttpClientResponse 对象,封装了Http响应信息
def response = client.postMultipart("http://localhost:8080/headers", {
    // delegate -> HttpClientMultipart
    // HttpClientMultipart.text() 用于添加文本类型的part
    text("textPartName", "textPartValue", [header1: "value1", header2: "value2"])
})
// HttpClientResponse.textBody() 可以将服务器返回结果转成文本
def textResult = response.textBody()
$console.log("服务器返回结果:$textResult")
// 断言返回结果为"/headers"
$assert.assertTrue(textResult == "/headers", "返回结果为 /headers")
// HttpServer.stop() 用于停止服务器.
server.stop()